// ________________________________________________________________________________________________
// import section
package rs.ac.bg.etf.pp1;

import rs.ac.bg.etf.pp1.ast.*;
import rs.ac.bg.etf.pp1.util.Log4J;



// ________________________________________________________________________________________________
// directive section

parser code {:
    private Compiler.State state = null;
	private boolean fatalErrorDetected = false;

    private Token currToken = null;
    private TokenList errorTokens = new TokenList();

    public void finishConstruction( Compiler.State state )
    {
        this.state = state;
    }


    // check if there are fatal parse errors
    public boolean noFatalErrors() { return !fatalErrorDetected; }


    @Override
    protected int error_sync_size() { return 1; }

    // NOTE: info is currently unused
    @Override
    public void report_fatal_error( String message, Object info )
    {
        fatalErrorDetected = true;

        report_error( message, info );
        done_parsing();
    }

    // NOTE: info is currently unused
    @Override
    public void report_error( String message, Object info )
    {
        // ignore default messages (lr parser messages that can't be replaced without rewriting that part of the parser in the .jar file)
        if( message == null
        || "Syntax error".equals( message )
        || "Couldn't repair and continue parse".equals( message ) )
        {
            message = null;
        }

        // if a fatal error is detected and the message is null, set the default fatal error message
        if( fatalErrorDetected && message == null )
        {
            message = "Fatal error detected, parsing stopped";
        }

        // if the error token list is empty, add the first error token to the error token list
        if( errorTokens.size() == 0 ) errorTokens.add( currToken );

        // if the message is not ready, don't report yet
        if( message == null ) return;
        

        // if the error is not fatal and the error token list was not empty beforehand, remove excess non-error tokens
        // +   ignored tokens are not counted towards the quota while removing
        // +   the last token after this will definitely be an error token
        if( !fatalErrorDetected && errorTokens.size() > 1 )
        {
            int errorTokenCount = error_sync_size() + 1;
            for( int i = 0; i <= errorTokenCount;   )
            {
                // if the list's last token is not an ignored token, increment the counter of excess non-error non-ignored tokens
                if( !errorTokens.getLast().isIgnored() ) i++;

                // if this is the first non-excess token at the end of the list or if there is only one element to show, break
                if( i == errorTokenCount + 1 || errorTokens.size() <= 1 ) break;
                
                // remove the list's last element
                errorTokens.removeLast();
            }
        }

        // calculate the from and to error tokens' indexes
        int tokenFromIdx = CompilerError.NO_INDEX;
        int tokenToIdx = CompilerError.NO_INDEX;

        if( errorTokens.size() > 0 )
        {
            tokenFromIdx = ( ( Token )errorTokens.getFirst() )._idx();
            if( !fatalErrorDetected ) tokenToIdx = ( ( Token )errorTokens.getLast() )._idx();
            else                      tokenToIdx = tokenFromIdx;

            // clear all error tokens in the current syntax error
            errorTokens.clear();
        }


        // report the syntax error
        state._errors().add( CompilerError.SYNTAX_ERROR, message, tokenFromIdx, tokenToIdx );
    }
    

    // this function is not designed for advanced error logging
    // +   ignore the <message> and current <token/ast node>
    protected void unrecovered_syntax_error( Token cur_token )
    {
        report_fatal_error( null, cur_token );
    }

    // this function is not designed for advanced error logging
    // +   ignore the <message> and current <token/ast node>
    protected void syntax_error( Token cur_token )
    {
        report_error( null, cur_token );
    }

:}

scan with {:
    while( true )
    {
        // replace the current token with the next token from the scanner
    	currToken = ( Token )( getScanner().next_token() );

        // if the parser is currently resolving an error, save the token
        if( errorTokens.size() > 0 )
        {
            errorTokens.add( currToken );
        }

        // if the current token should not be ignored
        if( !currToken.isIgnored() )
        {
            // log the current token and break
            state._logger().log( Log4J.INFO, currToken.toString(), true );
            break;
        }
    }

    // if the current token is invalid
    if( currToken.isInvalid() )
    {
        // report it as a lexer error
        state._errors().add( CompilerError.LEXICAL_ERROR, "Bad token", currToken._idx(), currToken._idx()  );
    }

    // return the current token
    return currToken;
:}





// ________________________________________________________________________________________________
// language characters


// keywords [terminals]
terminal PROGRAM_K, CLASS_K, /*STRUCT_K,*/ RECORD_K /*,ENUM_K*/;
terminal STATIC_K, CONST_K, VOID_K, NULL_K;
terminal /*ABSTRACT_K,*/ EXTENDS_K /*,IMPLEMENTS_K*/, THIS_K, SUPER_K;
terminal IF_K, ELSE_K, SWITCH_K, CASE_K, DO_K, WHILE_K /*,FOR_K*/;
terminal /*DEFAULT_K,*/ BREAK_K, CONTINUE_K, RETURN_K, GOTO_K;
terminal NEW_K, /*DELETE_K,*/ PRINT_K, READ_K;

// operators [terminals]
//       +     -      *    /    %
terminal plus, minus, mul, div, perc;
//       ==  !=  >   >=  <   <=  &&   ||    !
terminal eq, ne, gt, ge, lt, le, and, or/*, not*/;
//       =       ++        --
terminal assign, plusplus, minusminus;
//       ;        ,      .    {       }       (       )       [         ]         ?          :
terminal semicol, comma, dot, lbrace, rbrace, lparen, rparen, lbracket, rbracket, /*qmark,*/ colon;

// literals [terminals]
terminal String int_lit;    // 0   10   12012
terminal String bool_lit;   // true   false
terminal String char_lit;   // 'a'   '-'

// identifiers [terminals]
terminal String ident;         // asdf0_13

// ignored tokens [terminals]
// +   whitespaces do not include newlines!
terminal newline, whitespace, line_comment, inline_comment;
// error token in lexer [terminals]
// +   used instead of error, since then the parser doesn't skip reporting it as an error
terminal invalid;



// declarations [nonterminals]
nonterminal Program;
nonterminal rs.ac.bg.etf.pp1.Symbol ProgramType;
nonterminal GlobalDeclList;
nonterminal GlobalDecl;

nonterminal ClassDecl;
nonterminal rs.ac.bg.etf.pp1.Symbol ClassDeclType;
nonterminal ClassDeclBody;
nonterminal MethodDeclList;

nonterminal RecordDecl;
nonterminal rs.ac.bg.etf.pp1.Symbol RecordDeclType;
nonterminal RecordDeclBody;

nonterminal MethodDecl;
nonterminal rs.ac.bg.etf.pp1.Symbol MethodDeclType;
nonterminal MethodDeclBody;
nonterminal rs.ac.bg.etf.pp1.props.JumpProp MethodDeclCode;   // contains the starting addresses of the labels declared inside the method
nonterminal FormPars;
nonterminal FormParsScope;
nonterminal FormParsList;
nonterminal rs.ac.bg.etf.pp1.Symbol FormParam;
nonterminal rs.ac.bg.etf.pp1.Symbol FormParamType;
nonterminal VarDeclList;

nonterminal VarDecl;
nonterminal rs.ac.bg.etf.pp1.Symbol VarDeclType;
nonterminal VarIdentList;
nonterminal rs.ac.bg.etf.pp1.Symbol VarIdent;

nonterminal ConstDecl;
nonterminal rs.ac.bg.etf.pp1.Symbol ConstDeclType;
nonterminal ConstInitList;
nonterminal rs.ac.bg.etf.pp1.Symbol ConstInit;



// statements [nonterminals]
nonterminal StatementList;
nonterminal Statement;
nonterminal StmtLabel;

nonterminal Stmt;
nonterminal rs.ac.bg.etf.pp1.props.JumpProp IfScope;
nonterminal IfCondition;
nonterminal IfStmt;
nonterminal ElseStmt;
nonterminal rs.ac.bg.etf.pp1.props.JumpProp DoWhileScope;
nonterminal DoWhileStmt;
nonterminal DoWhileCondition;
nonterminal rs.ac.bg.etf.pp1.props.JumpProp SwitchScope;
nonterminal SwitchExpr;
nonterminal DesignatorStmt;
nonterminal CaseList;
nonterminal Case;
nonterminal CaseScope;

nonterminal ActPars;
nonterminal ActParsScope;
nonterminal ActParsList;
nonterminal rs.ac.bg.etf.pp1.Symbol ActParam;

nonterminal Condition;
nonterminal CondTermList;
nonterminal rs.ac.bg.etf.pp1.props.JumpProp CondTerm;
nonterminal CondNest;
nonterminal CondFact;
nonterminal CondTermScope;

nonterminal rs.ac.bg.etf.pp1.Symbol Expr;
nonterminal rs.ac.bg.etf.pp1.Symbol Addition;
nonterminal rs.ac.bg.etf.pp1.Symbol Term;

nonterminal rs.ac.bg.etf.pp1.Symbol Factor;
nonterminal rs.ac.bg.etf.pp1.Symbol MethodCall;
nonterminal rs.ac.bg.etf.pp1.Symbol Designator;



// miscellaneous [nonterminals]
nonterminal rs.ac.bg.etf.pp1.Symbol ReturnType;  // value, type
nonterminal rs.ac.bg.etf.pp1.Symbol Type;        // value, type
nonterminal rs.ac.bg.etf.pp1.Symbol Literal;     // value, type

nonterminal rs.ac.bg.etf.pp1.Symbol Assignop;
nonterminal rs.ac.bg.etf.pp1.Symbol Aorop;
nonterminal rs.ac.bg.etf.pp1.Symbol Relop;
nonterminal rs.ac.bg.etf.pp1.Symbol Addop;
nonterminal rs.ac.bg.etf.pp1.Symbol Mulop;



// associativity
precedence left ELSE_K;        // left associative operation => 'else' is paired with the closest left 'if'

// starting non-terminal
start with Program;





// ________________________________________________________________________________________________
// language rules


// program ident { }
// program ident { method method method }
// program ident constdl constdl vardl vardl classdl { }
// program ident constdl constdl vardl vardl classdl { method method method }
Program ::= (Program_Plain) ProgramType GlobalDeclList lbrace MethodDeclList rbrace;

// program my_program
ProgramType ::= (ProgramType_Plain) PROGRAM_K ident:ProgramName;

// <epsilon>
// constdl constdl vardl vardl classdl
GlobalDeclList ::= (GlobalDeclList_Tail ) GlobalDeclList GlobalDecl;
GlobalDeclList ::= (GlobalDeclList_Empty) ;

// constdl
// vardl
// classdl
// recorddl
GlobalDecl ::= (GlobalDecl_Const ) ConstDecl;
GlobalDecl ::= (GlobalDecl_Var   ) VarDecl;
GlobalDecl ::= (GlobalDecl_Class ) ClassDecl;
GlobalDecl ::= (GlobalDecl_Record) RecordDecl;



// class A { }
// class A { { method method method } }
// class A extends B { vardl vardl vardl vardl }
// class A extends B { vardl vardl vardl vardl { method method method } }
ClassDecl ::= (ClassDecl_Plain) ClassDeclType lbrace ClassDeclBody rbrace;

// class A
// class A extends B
ClassDeclType ::= (ClassDeclType_Plain  ) CLASS_K ident:ClassName;
ClassDeclType ::= (ClassDeclType_Extends) CLASS_K ident:ClassName EXTENDS_K Type;
ClassDeclType ::= (ClassDeclType_Err    ) CLASS_K error {: parser.report_error( "Bad class declaration", null ); :};

// <epsilon>
// { method method method }
// vardl vardl vardl vardl
// vardl vardl vardl vardl { method method method }
ClassDeclBody ::= (ClassDeclBody_Vars       ) VarDeclList;
ClassDeclBody ::= (ClassDeclBody_VarsMethods) VarDeclList lbrace MethodDeclList rbrace;

// <epsilon>
// method method method
MethodDeclList ::= (MethodDeclList_Tail ) MethodDeclList MethodDecl;
MethodDeclList ::= (MethodDeclList_Empty) ;



// record A { }
// record A { vardl vardl vardl vardl }
RecordDecl ::= (RecordDecl_Plain) RecordDeclType lbrace RecordDeclBody rbrace;

// record A
RecordDeclType ::= (RecordDeclType_Plain) RECORD_K ident:RecordName;
RecordDeclType ::= (RecordDeclType_Err  ) RECORD_K error {: parser.report_error( "Bad record declaration", null ); :};

// <epsilon>
// vardl vardl vardl vardl
RecordDeclBody ::= (RecordDeclBody_Vars) VarDeclList;



// void foo() { }
// void foo() { statement statement }
// void foo() vardl vardl { }
// void foo() vardl vardl { statement statement }
//      foo() vardl vardl { statement statement }   -- constructor
// void foo( int a, char c, Node Array[] ) { }
// void foo( int a, char c, Node Array[] ) { statement statement }
// void foo( int a, char c, Node Array[] ) vardl vardl { }
// void foo( int a, char c, Node Array[] ) vardl vardl { statement statement }
MethodDecl ::= (MethodDecl_Plain) MethodDeclType FormParsScope FormPars rparen MethodDeclBody VarDeclList MethodDeclCode lbrace StatementList rbrace;

// void foo
// A foo
// foo   -- constructor
MethodDeclType ::= (MethodDeclType_Plain) ReturnType ident:MethodName;
MethodDeclType ::= (MethodDeclType_Empty)            ident:MethodName;

// action symbol for opening a new scope
MethodDeclBody ::= (MethodDeclBody_Plain) ;

// action symbol for the beginning of the method code
MethodDeclCode ::= (MethodDeclCode_Plain) ;

// <epsilon>
// int ident, Node Array[], char c
FormPars ::= (FormPars_List ) FormParsList;
FormPars ::= (FormPars_Empty) ;

// action symbol for opening a new scope
FormParsScope ::= (FormParsScope_Plain) lparen;

// int a, char c, Node Array[]
FormParsList ::= (FormParsList_Init)                    FormParam;
FormParsList ::= (FormParsList_Tail) FormParsList comma FormParam;

// int a, char c, Node Array[]
FormParam ::= (FormParam_Plain) FormParamType VarIdent;
FormParam ::= (FormParam_Err  ) error {: parser.report_error( "Bad formal parameter", null ); :};

// int
// Node
FormParamType ::= (FormParamType_Plain) Type;

// <epsilon>
// vardl vardl vardl vardl
VarDeclList ::= (VarDeclList_VarDecl) VarDeclList VarDecl;
VarDeclList ::= (VarDeclList_Empty  ) ;



// int a, b[], c;
// A a1, a2;
// static int a, b[], c;   // the static keyword is only allowed inside a class declaration!
// static A a1, a2;
VarDecl ::= (VarDecl_Plain) VarDeclType VarIdentList semicol;

// int
// static A
VarDeclType ::= (VarDeclType_Plain )          Type;
VarDeclType ::= (VarDeclType_Static) STATIC_K Type;
VarDeclType ::= (VarDeclType_Err   ) error {: parser.report_error( "Bad variable type", null ); :};

// a
// a, b[], c
VarIdentList ::= (VarIdentList_VarIdent)                    VarIdent;
VarIdentList ::= (VarIdentList_Tail    ) VarIdentList comma VarIdent;

// a
// b[]
VarIdent ::= (VarIdent_Ident) ident:VarName;
VarIdent ::= (VarIdent_Array) ident:VarName lbracket rbracket;
VarIdent ::= (VarIdent_Err  ) error {: parser.report_error( "Bad variable name", null ); :};



// const int a = 5, b = 6, c = 11;
ConstDecl ::= (ConstDecl_Plain) ConstDeclType ConstInitList semicol;

// const int
ConstDeclType ::= (ConstDeclType_Plain) CONST_K Type;
ConstDeclType ::= (ConstDeclType_Err  ) CONST_K error {: parser.report_error( "Bad constant type", null ); :};

// a = 5, b = 6, c = 11
ConstInitList ::= (ConstInitList_Init)                     ConstInit;
ConstInitList ::= (ConstInitList_Tail) ConstInitList comma ConstInit;

// a = 5
ConstInit ::= (ConstInit_Plain) ident:IdentName Assignop Literal;
ConstInit ::= (ConstInit_Err  ) error {: parser.report_error( "Bad initialization", null ); :};






// <epsilon>
// labstatement statement { statement statement } statement { }
StatementList ::= (StatementList_Tail ) StatementList Statement;
StatementList ::= (StatementList_Empty) ;

// label_01:stmt
// {}
// { label1:statement label2:statement label3:statement }
Statement ::= (Statement_Plain)           Stmt;
Statement ::= (Statement_Label) StmtLabel Stmt;
Statement ::= (Statement_Scope) lbrace StatementList rbrace;
Statement ::= (Statement_Err  ) error {: parser.report_error( "Bad statement", null ); :};

// action symbol for defining a label
StmtLabel ::= (StmtLabel_Plain) ident:Label colon;

// ident.ident[ expr ] = expr;
// ident.ident[ expr ]( );
// ident.ident[ expr ]( expr, expr, expr );
// ident.ident[ expr ]++;
// ident.ident[ expr ]--;
//
// if( condition ) statement
// if( condition ) statement else statement
// do statement while( condition );
// switch( expr ) { }
// switch( expr ) { case 1: statement statement statement   case 2: statement statement }
// break;
// continue;
// return;
// return expr;
// goto label_01;
//
// read( ident.ident[ expr ] );
// print( ident.ident[ expr ], 2 );
//
// ;
Stmt ::= (Stmt_Designator ) DesignatorStmt semicol;
Stmt ::= (Stmt_If         ) IfScope lparen IfCondition rparen IfStmt;
Stmt ::= (Stmt_IfElse     ) IfScope lparen IfCondition rparen IfStmt ELSE_K ElseStmt;
Stmt ::= (Stmt_DoWhile    ) DoWhileScope DoWhileStmt WHILE_K lparen DoWhileCondition rparen semicol;
Stmt ::= (Stmt_Switch     ) SwitchScope lparen SwitchExpr rparen lbrace CaseList rbrace;
Stmt ::= (Stmt_Break      ) BREAK_K       semicol;
Stmt ::= (Stmt_Continue   ) CONTINUE_K    semicol;
Stmt ::= (Stmt_Return     ) RETURN_K      semicol;
Stmt ::= (Stmt_ReturnExpr ) RETURN_K Expr semicol;
Stmt ::= (Stmt_Goto       ) GOTO_K ident:Label semicol;
Stmt ::= (Stmt_Read       ) READ_K lparen Designator rparen semicol;
Stmt ::= (Stmt_Print      ) PRINT_K lparen Expr                        rparen semicol;
Stmt ::= (Stmt_PrintFormat) PRINT_K lparen Expr comma int_lit:MinWidth rparen semicol;
Stmt ::= (Stmt_Semicolon  ) semicol;

// action symbols for opening a new scope and the if-statement's jump instructions
IfScope ::= (IfScope_Plain) IF_K;
IfCondition ::= (IfCondition_Plain) Condition;
IfStmt ::= (IfStmt_Plain) Statement;
ElseStmt ::= (ElseStmt_Plain) Statement;

// action symbols for opening a new scope and the do-while-statement's jump instructions
DoWhileScope ::= (DoWhileScope_Plain) DO_K;
DoWhileStmt ::= (DoWhileStmt_Plain) Statement;
DoWhileCondition ::= (DoWhileCondition_Plain) Condition;

// action symbols for opening a new scope and the switch-statement's jump instructions
SwitchScope ::= (SwitchScope_Plain) SWITCH_K;
SwitchExpr ::= (SwitchExpr_Plain) Expr;

// ident.ident[ expr ] = expr
// ident.ident[ expr ]( )
// ident.ident[ expr ]( expr, expr, expr )
// ident.ident[ expr ]++
// ident.ident[ expr ]--
DesignatorStmt ::= (DesignatorStmt_Assign    ) Designator Assignop Expr;
DesignatorStmt ::= (DesignatorStmt_Call      ) MethodCall ActParsScope ActPars rparen;
DesignatorStmt ::= (DesignatorStmt_Plusplus  ) Designator plusplus;
DesignatorStmt ::= (DesignatorStmt_Minusminus) Designator minusminus;

// <epsilon>
// case 1: statement statement statement   case 2: statement statement
CaseList ::= (CaseList_Tail ) CaseList Case;
CaseList ::= (CaseList_Empty) ;

// case 1: statement statement statement
// case 2: 
// case 3: {}
Case ::= (Case_Plain) CaseScope StatementList;

// action symbols for opening a new scope and the case-statement's jump instructions
CaseScope ::= (CaseScope_Plain) CASE_K int_lit:CaseNum colon;



// <epsilon>
// expr
// expr, expr, expr
ActPars ::= (ActPars_Plain) ActParsList;
ActPars ::= (ActPars_Empty) ;

// action symbol for opening a new scope
ActParsScope ::= (ActParsScope_Plain) lparen;

// expr
// expr, expr, expr
ActParsList ::= (ActParsList_Expr)                   ActParam;
ActParsList ::= (ActParsList_Tail) ActParsList comma ActParam;

// expr
ActParam ::= (ActParam_Plain) Expr;

// bool   |   expr < expr   |   expr != expr
// ( expr == expr )
// ( expr >= expr || expr == expr && expr >= expr )   // 'and' has greater priority than 'or' implicitly
//       .A                .C        .H         .F             .H             .K   .K        .L        .P   // jumpIfNot(X) to .(&Y)
// if(   M && N   ||   ((( A && B || C ))) && ( D && E || F || G && Q )   ||  H && I && J || K && R || L   )(&&)   O   else(||)   P;
//            .O                .D                   .O   .O        .O                  .O        .O        // jumpIf(X) to .(&Y)
Condition ::= (Condition_Single ) CondTerm;
Condition ::= (Condition_Multi  ) CondTermList;

// ((( true )))                                       // the parentheses belong to the expression! (not to the condition)
// bool && b > c
// ((( ((bool)) && (( (b) > (c) )) )))
CondTermList ::= (CondTermList_Aor ) CondTerm     CondTermScope CondTerm;
CondTermList ::= (CondTermList_Tail) CondTermList CondTermScope CondTerm;

// ((( cterm && cterm || cterm )))   |   expr   |   expr < expr   |   expr != expr
CondTerm ::= (CondTerm_Fact) CondFact;
CondTerm ::= (CondTerm_Nest) CondNest;

// ((( cterm && cterm || cterm )))
CondNest ::= (CondNest_Head) lparen CondTermList rparen;
CondNest ::= (CondNest_Tail) lparen CondNest     rparen;

// expr   |   expr < expr   |   expr != expr
CondFact ::= (CondFact_Expr ) Expr;
CondFact ::= (CondFact_Relop) Expr Relop Expr;

// action symbols for finding out the next term's starting address
CondTermScope ::= (CondTermScope_Plain) Aorop;

// +term - term + term + term
Expr ::= (Expr_Addition) Addition;
Expr ::= (Expr_Err     ) error {: parser.report_error( "Bad expression", null ); :};

// term
// +term
// -term
// term + term - term + term
// -term + term - term + term
// +term + term + term + term
Addition ::= (Addition_Term )                Term;
Addition ::= (Addition_STerm)          Addop Term;
Addition ::= (Addition_Tail ) Addition Addop Term;

// factor
// factor*factor*factor
Term ::= (Term_Factor)            Factor;
Term ::= (Term_Tail  ) Term Mulop Factor;



// ident.ident[ expr ]
// ident.ident[ expr ]( )
// ident.ident[ expr ]( expr, expr, expr )
// 1202 | 'c' | true
// new Object
// new Array[ expr ]
// ((( expr )))
Factor ::= (Factor_Designator ) Designator;
Factor ::= (Factor_MethodCall ) MethodCall ActParsScope ActPars rparen;
Factor ::= (Factor_Literal    ) Literal;
Factor ::= (Factor_NewVar     ) NEW_K Type;
Factor ::= (Factor_NewArray   ) NEW_K Type lbracket Expr rbracket;
Factor ::= (Factor_Expr       ) lparen Expr rparen;

// ident.ident[ expr ]( expr, expr, expr )
MethodCall ::= (MethodCall_Plain) Designator;

// ident
// this.ident
// super()
// null
// ident.ident
// ident[ expr ]
// ident.ident.ident[ expr ].ident
// ident.ident.ident[ expr ].ident[ expr ]
Designator ::= (Designator_Ident  ) ident:Name;
Designator ::= (Designator_This   ) THIS_K;
Designator ::= (Designator_Super  ) SUPER_K;
Designator ::= (Designator_Null   ) NULL_K;
Designator ::= (Designator_Member ) Designator dot ident:Name;
Designator ::= (Designator_ArrElem) Designator lbracket Expr rbracket;





// void | type
ReturnType ::= (ReturnType_Void ) VOID_K:TypeName;
ReturnType ::= (ReturnType_Ident) ident :TypeName;
// int | bool | char | ident
Type ::= (Type_Ident) ident:TypeName;
// 1202 | 'c' | true
Literal ::= (Literal_Int ) int_lit :Literal;
Literal ::= (Literal_Char) char_lit:Literal;
Literal ::= (Literal_Bool) bool_lit:Literal;

// =
Assignop ::= (Assignop_Assign) assign:Assignop;
// &&  |  ||
Aorop ::= (Aorop_And) and:Aorop;
Aorop ::= (Aorop_Or ) or :Aorop;
// ==  |  !=  |  >  |  >=  |  <  |  <=
Relop ::= (Relop_Eq) eq:Relop;
Relop ::= (Relop_Ne) ne:Relop;
Relop ::= (Relop_Gt) gt:Relop;
Relop ::= (Relop_Ge) ge:Relop;
Relop ::= (Relop_Lt) lt:Relop;
Relop ::= (Relop_Le) le:Relop;
// +  |  -
Addop ::= (Addop_Plus ) plus :Addop;
Addop ::= (Addop_Minus) minus:Addop;
// *  |  /  |  %
Mulop ::= (Mulop_Mul ) mul :Mulop;
Mulop ::= (Mulop_Div ) div :Mulop;
Mulop ::= (Mulop_Perc) perc:Mulop;






